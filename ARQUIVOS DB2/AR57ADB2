       IDENTIFICATION DIVISION.
       PROGRAM-ID. AR57ADB2.
       AUTHOR.   RODRIGO.
      *---------------------------------------------------------------
      * PROGRAMA PARA ATUALIZAR FUNCIONARIOS (DB2)
      * USANDO
      *
      *---------------------------------------------------------------
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *O COMANDO ACIMA REFERESE AO SISTEMA DECIMAL
      *===============================================================
      *ESTA SESAO SERVE PARA DECLARAR ARQUIVOS
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MOVIMENTO ASSIGN TO MOVIMENT
              FILE STATUS IS STATUS-MOVIMENTO.
           SELECT RELATO    ASSIGN TO RELATO.
      *===============================================================
      * DECLARACAO DE ARQUIVOS
       DATA DIVISION.
       FILE SECTION.
      *----------------------------------------------------------
      * O NOME MOVIMENTO VEM DO SELECT
       FD MOVIMENTO.
       01  REG-MOVIMENTO.
           02 COD-FUN-M              PIC X(05).
           02 NOME-FUN-M             PIC X(30).
           02 DEP-FUN-M              PIC X(03).
           02 SAL-FUN-M              PIC 9(05)V99.
           02 ADMISS-FUN-M           PIC X(10).
           02 FILHOS-FUN-M           PIC 9(02).
           02 TIPO-M                 PIC X(01).
      * SEMPRE QUANDO TRABALHARMOS COM ARQUIVOS TEMOS QUE DECLARAR
      *OS CAMPOS NESTA SECTION, NAO PODEMOS COLOCAR VALUE NESTA SECTION
      * LRECL 58
      *===============================================================
      *RESERVANDO 90 CARACTERES
       FD  RELATO.
       01  REG-RELATO                PIC X(90).
      *
       WORKING-STORAGE SECTION.
      * DECLARACAO DE VARIAVEIS
       01  CABEC1.
           02 FILLER            PIC X(10) VALUE SPACES.
           02 FILLER            PIC X(51) VALUE
              'ATUALIZACAO DE CADASTROS DE FUNCIONARIOS -MES BASE '.
           02 MES-CABEC1        PIC X(09) VALUE SPACES.
           02 FILLER            PIC X(01) VALUE '/'.
           02 ANO-CABEC1        PIC 9(02) VALUE ZEROS.
           02 FILLER            PIC X(08) VALUE '   PAG. '.
           02 NUM-PAG           PIC 9(02) VALUE 0.
       01  CABEC2.
           02 FILLER            PIC X(03) VALUE SPACES.
           02 FILLER            PIC X(18) VALUE 'IMAGEM DO REGISTRO'.
           02 FILLER            PIC X(43) VALUE SPACES.
           02 FILLER            PIC X(10) VALUE 'OCORRENCIA'.
       01  DETALHE.
           02 FILLER                     PIC X(03) VALUE SPACES.
           02 IMAGEM-DET.
               03 COD-FUN-DET            PIC X(05).
               03 NOME-FUN-DET           PIC X(30).
               03 DEP-FUN-DET            PIC X(03).
               03 SAL-FUN-DET            PIC 9(05)V99.
               03 ADMISS-FUN-DET         PIC X(10).
               03 FILHOS-FUN-DET         PIC 9(02).
               03 TIPO-M-DET             PIC X(01).
           02 FILLER                     PIC X(03) VALUE SPACES.
           02 OCORRENCIA-DET             PIC X(15) VALUE SPACES.
       01  RODAPE-1.
           02 FILLER                    PIC X(26) VALUE SPACES.
           02 FILLER                    PIC X(34) VALUE
              'ESTATISTICA FINAL DE PROCESSAMENTO'.
       01  RODAPE-2.
           02 FILLER                    PIC X(26) VALUE SPACES.
           02 TITULO-RODAPE             PIC X(34) VALUE SPACES.
           02 TOTAL-RODAPE              PIC 999   VALUE 0.
           EXEC SQL
                INCLUDE FUNCDB2
           END-EXEC.
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.
       77  STATUS-MOVIMENTO             PIC X(02)  VALUE SPACES.
       77  TOT-ALTERADOS                PIC 999 VALUE 0.
       77  TOT-INCLUIDOS                PIC 999 VALUE 0.
       77  TOT-EXCLUIDOS                PIC 999 VALUE 0.
       77  TOT-REJEITADOS               PIC 999 VALUE 0.
       77  CONTALIN                     PIC 99  VALUE 0.
       77  SQLCODE-EDIT                 PIC -ZZ9 VALUE ZEROS.
       01  DATA-SIST.
           02 ANO-SIST                  PIC 99    VALUE 0.
           02 MES-SIST                  PIC 99    VALUE 0.
           02 DIA-SIST                  PIC 99    VALUE 0.
       77  INDICATOR-ADMISSAO           PIC S9(04) COMP VALUE 0.
       77  INDICATOR-FILHOS             PIC S9(04) COMP VALUE 0.
      *===============================================================
       PROCEDURE DIVISION.
       000-PRINCIPAL SECTION.
       001-PRINCIPAL.
           PERFORM 101-INICIAR.
           PERFORM 201-PROCESSAR UNTIL STATUS-MOVIMENTO = '10'.
      *   (UNTIL)EXECUTA ATE QUE ESTATUS SEJA = 10
           PERFORM 901-FINALIZAR.
           STOP RUN.
       099-FIM-PRINCIPAL.
           EXIT.
      *==============================================================
      * MODULO INICIAR
      *==============================================================
       100-INICIAR SECTION.
       101-INICIAR.
           OPEN INPUT MOVIMENTO.
           EVALUATE STATUS-MOVIMENTO
              WHEN '00'
                  CONTINUE
              WHEN '35'
                  DISPLAY 'ARQUIVO DO MOVIMENTO NAO ENCONTRADO '
                  MOVE 8 TO RETURN-CODE
                  STOP RUN
              WHEN OTHER
                  DISPLAY 'ERRO ' STATUS-MOVIMENTO
                  ' NO COMANDO OPEN DO MOVIMENTO'
                  MOVE 12 TO RETURN-CODE
                  STOP RUN
           END-EVALUATE.
           OPEN OUTPUT RELATO.
           ACCEPT DATA-SIST  FROM     DATE.
           MOVE ANO-SIST     TO  ANO-CABEC1.
           EVALUATE MES-SIST
               WHEN 1
                   MOVE 'JANEIRO'     TO MES-CABEC1
               WHEN 2
                   MOVE 'FEVEREIRO'   TO MES-CABEC1
               WHEN 3
                   MOVE 'MARCO'       TO MES-CABEC1
               WHEN 4
                   MOVE 'ABRIL'       TO MES-CABEC1
               WHEN 5
                   MOVE 'MAIO'        TO MES-CABEC1
               WHEN 6
                   MOVE 'JUNHO'       TO MES-CABEC1
               WHEN 7
                   MOVE 'JULHO'       TO MES-CABEC1
               WHEN 8
                   MOVE 'AGOSTO'      TO MES-CABEC1
               WHEN 9
                   MOVE 'SETEMBRO'    TO MES-CABEC1
               WHEN 10
                   MOVE 'OUTUBRO'     TO MES-CABEC1
               WHEN 11
                   MOVE 'NOVEMBRO'    TO MES-CABEC1
               WHEN 12
                   MOVE 'DEZEMBRO'    TO MES-CABEC1
           END-EVALUATE.
           PERFORM 301-LER-MOVIMENTO.
       199-FIM-INICIAR.
           EXIT.
      *---------------------------------------------------------------
      *   MODULO PROCESSAR
      *---------------------------------------------------------------
       200-PROCESSAR SECTION.
       201-PROCESSAR.
           EVALUATE TIPO-M
               WHEN 'A'
                   PERFORM 501-ALTERAR
               WHEN 'I'
                   PERFORM 601-INCLUIR
               WHEN 'E'
                   PERFORM 701-EXCLUIR
               WHEN OTHER
                   MOVE REG-MOVIMENTO   TO IMAGEM-DET
                   MOVE 'TIPO INVALIDO' TO OCORRENCIA-DET
                   PERFORM 801-IMPRIMIR
           END-EVALUATE.
           PERFORM 301-LER-MOVIMENTO.
       299-FIM-PROCESSAR.
           EXIT.
      *===============================================================
      * MODULO LER-MOVIMENTO                                         *
      *===============================================================
       300-LER-MOVIMENTO SECTION.
       301-LER-MOVIMENTO.
           READ MOVIMENTO.
           EVALUATE STATUS-MOVIMENTO
              WHEN '00'
                  CONTINUE
              WHEN '10'
                  CONTINUE
              WHEN OTHER
                  DISPLAY 'ERRO ' STATUS-MOVIMENTO
                  ' NO COMANDO READ DO MOVIMENTO'
                  MOVE 12 TO RETURN-CODE
                  STOP RUN
           END-EVALUATE.
       399-FIM-LER-MOVIMENTO.
           EXIT.
      *===============================================================
      * MODULO LER-FUNCIONARIOS                                      *
      *===============================================================
       400-LER-FUNCIONARIOS SECTION.
       401-LER-FUNCIONARIOS.
           MOVE COD-FUN-M TO DB2-COD-FUN.
           EXEC SQL
               SELECT  *
               INTO    :DB2-COD-FUN,
                       :DB2-NOME-FUN,
                       :DB2-DEP-FUN,
                       :DB2-SAL-FUN,
                       :DB2-ADMISS-FUN :INDICATOR-ADMISSAO,
                       :DB2-FILHOS-FUN :INDICATOR-FILHOS
               FROM GPAR57.FUNCIONARIOS
               WHERE  COD_FUN = :DB2-COD-FUN
           END-EXEC.
           EVALUATE SQLCODE
              WHEN 0
                  CONTINUE
              WHEN 100
                  CONTINUE
              WHEN OTHER
                  MOVE SQLCODE TO SQLCODE-EDIT
                  DISPLAY 'ERRO ' SQLCODE-EDIT
                          'NO COMANDO SELECT '
                  MOVE 12 TO RETURN-CODE
                  STOP RUN
           END-EVALUATE.
       499-FIM-LER-FUNCIONARIOS.
           EXIT.
      *===============================================================
      * MODULO ALTERAR
      *===============================================================
       500-ALTERAR SECTION.
       501-ALTERAR.
           PERFORM 401-LER-FUNCIONARIOS.
           IF SQLCODE         = 100
               PERFORM 503-MOVER-FUNCIONARIO
               MOVE REG-MOVIMENTO        TO IMAGEM-DET
               MOVE 'NAO CADASTRADO'     TO OCORRENCIA-DET
               PERFORM 801-IMPRIMIR
               ADD 1                     TO TOT-REJEITADOS
           ELSE
               PERFORM 503-MOVER-FUNCIONARIO
               MOVE 'ANTES DA ALTERACAO' TO OCORRENCIA-DET
               PERFORM 801-IMPRIMIR
               PERFORM 502-ALTERAR
               PERFORM 503-MOVER-FUNCIONARIO
               MOVE 'APOS ALTERACAO'     TO OCORRENCIA-DET
               PERFORM 801-IMPRIMIR
               ADD 1                     TO TOT-ALTERADOS
           END-IF.
       502-ALTERAR.
           IF NOME-FUN-M NOT = SPACES
              MOVE NOME-FUN-M TO DB2-NOME-FUN-TEXT
              PERFORM 1001-CONTA-LETRAS
              EXEC SQL
                  UPDATE GPAR57.FUNCIONARIOS
                     SET  NOME_FUN = :DB2-NOME-FUN
                     WHERE COD_FUN = :DB2-COD-FUN
              END-EXEC
           END-IF.
           IF DEP-FUN-M  NOT = SPACES
               MOVE DEP-FUN-M  TO DB2-DEP-FUN
              EXEC SQL
                  UPDATE GPAR57.FUNCIONARIOS
                     SET  DEP_FUN = :DB2-DEP-FUN
                     WHERE COD_FUN = :DB2-COD-FUN
              END-EXEC
           END-IF.
           IF SAL-FUN-M IS NUMERIC
               MOVE SAL-FUN-M    TO DB2-SAL-FUN
               EXEC SQL
                  UPDATE GPAR57.FUNCIONARIOS
                     SET  SAL_FUN = :DB2-SAL-FUN
                     WHERE COD_FUN = :DB2-COD-FUN
               END-EXEC
           END-IF.
           IF ADMISS-FUN-M   NOT = SPACES
               MOVE ADMISS-FUN-M TO DB2-ADMISS-FUN
               EXEC SQL
                  UPDATE GPAR57.FUNCIONARIOS
                     SET  ADMISS_FUN = :DB2-ADMISS-FUN
                     WHERE COD_FUN = :DB2-COD-FUN
               END-EXEC
           END-IF.
           IF FILHOS-FUN-M  IS NUMERIC
               MOVE FILHOS-FUN-M   TO DB2-FILHOS-FUN
               EXEC SQL
                  UPDATE GPAR57.FUNCIONARIOS
                     SET  FILHOS_FUN = :DB2-FILHOS-FUN
                     WHERE COD_FUN = :DB2-COD-FUN
               END-EXEC
           END-IF.
           EVALUATE SQLCODE
               WHEN 0
                   CONTINUE
               WHEN -530
                   MOVE REG-MOVIMENTO        TO IMAGEM-DET
                   MOVE 'DEPTO INVALIDO'     TO OCORRENCIA-DET
                   PERFORM 801-IMPRIMIR
                   ADD 1                     TO TOT-REJEITADOS
               WHEN OTHER
                  MOVE SQLCODE TO SQLCODE-EDIT
                  DISPLAY 'ERRO ' SQLCODE-EDIT
                          'NO COMANDO UPDATE '
                  MOVE 12 TO RETURN-CODE
                  STOP RUN
           END-EVALUATE.
       503-MOVER-FUNCIONARIO.
           MOVE DB2-COD-FUN         TO        COD-FUN-DET.
           MOVE DB2-NOME-FUN-TEXT   TO        NOME-FUN-DET.
           MOVE DB2-DEP-FUN         TO        DEP-FUN-DET.
           MOVE DB2-SAL-FUN         TO        SAL-FUN-DET.
           MOVE DB2-ADMISS-FUN      TO        ADMISS-FUN-DET.
           MOVE DB2-FILHOS-FUN      TO        FILHOS-FUN-DET.
       599-FIM-ALTERAR.
           EXIT.
      *===============================================================
      * MODULO INCLUIR
      *===============================================================
       600-INCLUIR SECTION.
       601-INCLUIR.
           MOVE COD-FUN-M          TO DB2-COD-FUN.
           MOVE NOME-FUN-M         TO DB2-NOME-FUN-TEXT.
           PERFORM 1001-CONTA-LETRAS.
           MOVE DEP-FUN-M          TO DB2-DEP-FUN.
           MOVE SAL-FUN-M          TO DB2-SAL-FUN.
           MOVE FILHOS-FUN-M       TO DB2-FILHOS-FUN.
           EXEC SQL
                INSERT INTO GPAR57.FUNCIONARIOS
                   VALUES(:DB2-COD-FUN,
                          :DB2-NOME-FUN,
                          :DB2-DEP-FUN,
                          :DB2-SAL-FUN,
                          :DB2-ADMISS-FUN,
                          :DB2-FILHOS-FUN)
           END-EXEC.
           EVALUATE SQLCODE
              WHEN  0
                  MOVE  REG-MOVIMENTO    TO IMAGEM-DET
                  MOVE  'INCL. EFETUADA' TO OCORRENCIA-DET
                  PERFORM 801-IMPRIMIR
                  ADD  1  TO  TOT-INCLUIDOS
              WHEN -180
                  DISPLAY ' REG-MOVIMENTO ' REG-MOVIMENTO
                  STOP RUN
              WHEN -530
                   MOVE REG-MOVIMENTO        TO IMAGEM-DET
                   MOVE 'DEPTO INVALIDO'     TO OCORRENCIA-DET
                   PERFORM 801-IMPRIMIR
                   ADD 1                     TO TOT-REJEITADOS
              WHEN -803
                  MOVE  REG-MOVIMENTO    TO IMAGEM-DET
                  MOVE  'JA CADASTRADO'  TO OCORRENCIA-DET
                  PERFORM 801-IMPRIMIR
                  ADD 1 TO TOT-REJEITADOS
              WHEN OTHER
                  MOVE SQLCODE TO SQLCODE-EDIT
                  DISPLAY 'ERRO ' SQLCODE-EDIT
                          'NO COMANDO INSERT '
                  MOVE 12 TO RETURN-CODE
                  STOP RUN
           END-EVALUATE.
       699-FIM-INCLUIR.
           EXIT.
      *===============================================================
      * MODULO EXCLUIR
      *===============================================================
       700-EXCLUIR SECTION.
       701-EXCLUIR.
           PERFORM 401-LER-FUNCIONARIOS.
           IF SQLCODE =  0
               PERFORM 702-EXCLUIR
           ELSE
               MOVE REG-MOVIMENTO TO IMAGEM-DET
               MOVE 'NAO CADASTRADO' TO OCORRENCIA-DET
               PERFORM 801-IMPRIMIR
               ADD 1 TO TOT-REJEITADOS
           END-IF.
       702-EXCLUIR.
           EXEC SQL
               DELETE FROM GPAR57.FUNCIONARIOS
                 WHERE COD_FUN = :DB2-COD-FUN
           END-EXEC.
           IF  SQLCODE = 0
               PERFORM 503-MOVER-FUNCIONARIO
               MOVE 'EXCL. EFETUADA' TO OCORRENCIA-DET
               PERFORM 801-IMPRIMIR
               ADD 1 TO TOT-EXCLUIDOS
           ELSE
               MOVE SQLCODE TO SQLCODE-EDIT
               DISPLAY 'ERRO ' SQLCODE-EDIT
                       'NO COMANDO DELETE '
               MOVE 12 TO RETURN-CODE
               STOP RUN
           END-IF.
       799-FIM-EXCLUIR.
           EXIT.
      *===============================================================
      * MODULO IMPRIMIR
      *===============================================================
       800-IMPRIMIR  SECTION.
       801-IMPRIMIR.
           IF CONTALIN = 0 OR CONTALIN >= 50
               PERFORM 802-CABECALHO
           END-IF.
           WRITE REG-RELATO FROM DETALHE.
           ADD 1 TO CONTALIN.
       802-CABECALHO.
           ADD 1 TO NUM-PAG.
           WRITE REG-RELATO FROM CABEC1 AFTER PAGE.
           WRITE REG-RELATO FROM CABEC2 AFTER 3 LINES.
           MOVE SPACES TO REG-RELATO.
           WRITE REG-RELATO.
           MOVE 0 TO CONTALIN.
       899-FIM-IMPRESSAO.
           EXIT.
      *===============================================================
      * MODULO FINALIZAR
      *===============================================================
       900-FINALIZAR SECTION.
       901-FINALIZAR.
           ADD 1 TO NUM-PAG.
           WRITE REG-RELATO FROM CABEC1  AFTER PAGE.
           WRITE REG-RELATO FROM RODAPE-1 AFTER 2 LINES.

           MOVE 'TOTAL DE RESGISTROS ALTERADOS.....' TO TITULO-RODAPE.
           MOVE TOT-ALTERADOS   TO TOTAL-RODAPE.
           WRITE REG-RELATO FROM RODAPE-2 AFTER 2 LINES.

           MOVE 'TOTAL DE RESGISTROS INCLUIDOS.....' TO TITULO-RODAPE.
           MOVE TOT-INCLUIDOS   TO TOTAL-RODAPE.
           WRITE REG-RELATO FROM RODAPE-2.

           MOVE 'TOTAL DE RESGISTROS EXCLUIDOS.....' TO TITULO-RODAPE.
           MOVE TOT-EXCLUIDOS   TO TOTAL-RODAPE.
           WRITE REG-RELATO FROM RODAPE-2.

           MOVE 'TOTAL DE RESGISTROS REJEITADOS....' TO TITULO-RODAPE.
           MOVE TOT-REJEITADOS  TO TOTAL-RODAPE.
           WRITE REG-RELATO FROM RODAPE-2.
      *
           CLOSE RELATO.
      *
           CLOSE MOVIMENTO.
       999-FIM-FINALIZAR.
           EXIT.
      ***********************************************************
      *           MODULO PARA DIVERSAS ROTINAS
      ***********************************************************
       1000-DIVERSOS SECTION.
       1001-CONTA-LETRAS.
           MOVE  30  TO   DB2-NOME-FUN-LEN.
           PERFORM UNTIL DB2-NOME-FUN-TEXT(DB2-NOME-FUN-LEN:1)
                                     NOT = SPACES
               SUBTRACT   1   FROM DB2-NOME-FUN-LEN
           END-PERFORM.
