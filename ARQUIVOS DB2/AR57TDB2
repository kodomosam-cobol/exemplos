       IDENTIFICATION DIVISION.
       PROGRAM-ID. AR57TDB2.
       AUTHOR.     RODRIGO.
      *********************************************************
      *        PROGRAMA PARA LISTAR TODOS OS FUNCIONARIOS (DB2)     *
      * DAS TABELAS
      *********************************************************
      *
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      ************************************************************
       DATA DIVISION.
       FILE SECTION.
      *
       WORKING-STORAGE SECTION.
           EXEC SQL
               INCLUDE FUNCDB2
           END-EXEC.
      *
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
      *
           EXEC SQL
               DECLARE CURSOR_FUNC  CURSOR FOR
                   SELECT * FROM GPAR57.FUNCIONARIOS
           END-EXEC.
       77  SALARIO-EDIT          PIC ZZ.ZZ9,99   VALUE ZEROS.
       77  SQLCODE-EDIT          PIC -ZZ9        VALUE ZEROS.
       77  INDICATOR-ADMISSAO    PIC S9(4) COMP  VALUE ZEROS.
       77  INDICATOR-FILHOS      PIC S9(4) COMP  VALUE ZEROS.
       77  TEM-FILHOS            PIC X           VALUE 'S'.
      *****************************************************
       PROCEDURE DIVISION.
       000-PRINCIPAL SECTION.
       001-PRINCIPAL.
           PERFORM 101-INICIAR.
           PERFORM 201-PROCESSAR UNTIL SQLCODE = 100.
      *        CODIGO 100 SIGINIFICA FIM DE ARQUIVO
           PERFORM 901-FINALIZAR.
           STOP RUN.
       099-FIM-PRINCIPAL.
           EXIT.
      ***********************************
       100-INICIAR SECTION.
       101-INICIAR.
           EXEC SQL
               OPEN CURSOR_FUNC
           END-EXEC.
           EVALUATE SQLCODE
               WHEN 0
                   CONTINUE
               WHEN OTHER
                   MOVE SQLCODE TO SQLCODE-EDIT
                   DISPLAY 'ERRO ' SQLCODE-EDIT
                           ' NO COMANDO OPEN CURSOR '
                   MOVE 12 TO RETURN-CODE
                   STOP RUN
           END-EVALUATE.
           PERFORM 301-FETCH.
       199-FIM-INICIAR.
           EXIT.
      ************************************************
      *       MODULO PROCESSAR                       *
      ************************************************
       200-PROCESSAR SECTION.
       201-PROCESSAR.
           DISPLAY 'CODIGO = ' DB2-COD-FUN
           DISPLAY 'NOME = ' DB2-NOME-FUN-TEXT
           DISPLAY 'DEPARTAMENTO = ' DB2-DEP-FUN
           MOVE DB2-SAL-FUN TO SALARIO-EDIT
           DISPLAY 'SALARIO =  ' SALARIO-EDIT
           DISPLAY 'ADMISSAO = ' DB2-ADMISS-FUN
           IF TEM-FILHOS = 'S'
               DISPLAY 'FILHOS = ' DB2-FILHOS-FUN
           ELSE
               DISPLAY 'FILHOS = ---'
           END-IF.
           DISPLAY '************************************'
           PERFORM 301-FETCH.
       299-FIM-PROCESSAR.
           EXIT.
      *******************************************************
      *       MODULO FETCH                                  *
      *******************************************************
       300-FETCH SECTION.
       301-FETCH.
           MOVE SPACES TO DB2-NOME-FUN-TEXT.
           EXEC SQL
               FETCH CURSOR_FUNC
                  INTO :DB2-COD-FUN,
                       :DB2-NOME-FUN,
                       :DB2-DEP-FUN,
                       :DB2-SAL-FUN,
                       :DB2-ADMISS-FUN INDICATOR :INDICATOR-ADMISSAO,
                       :DB2-FILHOS-FUN :INDICATOR-FILHOS
           END-EXEC.
           IF INDICATOR-ADMISSAO = -1
               MOVE '----------' TO DB2-ADMISS-FUN
           END-IF.
           IF INDICATOR-FILHOS = -1
               MOVE 'N'          TO TEM-FILHOS
           END-IF.
           EVALUATE SQLCODE
               WHEN 0
                   CONTINUE
               WHEN 100
                   CONTINUE
               WHEN OTHER
                   MOVE SQLCODE TO SQLCODE-EDIT
                   DISPLAY 'ERRO ' SQLCODE-EDIT
                           ' NO COMANDO FETCH '
                   MOVE 12 TO RETURN-CODE
                   STOP RUN
           END-EVALUATE.
       399-FIM-FETCH. EXIT.
      *******************************************************
      *       MODULO FINALIZAR                              *
      *******************************************************
       900-FINALIZAR SECTION.
       901-FINALIZAR.
           EXEC SQL
                CLOSE CURSOR_FUNC
           END-EXEC.
